<!-- vim: syntax=markdown -->

<!-- livebook:{"persist_outputs":true} -->

# Advent of Code 2021

## The backstory

You're minding your own business on a ship at sea when the overboard alarm goes off! You rush to see if you can help. Apparently, one of the Elves tripped and accidentally sent the sleigh keys flying into the ocean!

Before you know it, you're inside a submarine the Elves keep ready for situations like this. It's covered in Christmas lights (because of course it is), and it even has an experimental antenna that should be able to track the keys if you can boost its signal strength high enough; there's a little meter that indicates the antenna's signal strength by displaying 0-50 stars.

Your instincts tell you that in order to save Christmas, you'll need to get all fifty stars by December 25th. Collect stars by solving puzzles. Two puzzles will be made available on each day in the Advent calendar; the second puzzle is unlocked when you complete the first. Each puzzle grants one star. Good luck!

## Day 1

As the submarine drops below the surface of the ocean, it automatically performs a sonar sweep of the nearby sea floor. On a small screen, the sonar sweep report (your puzzle input) appears: each line is a measurement of the sea floor depth as the sweep looks further and further away from the submarine.

## |> Part 1 - Sonar Sweep

For example, suppose you had the following report:

`199`
`200`
`208`
`210`
`200`
`207`
`240`
`269`
`260`
`263`

This report indicates that, scanning outward from the submarine, the sonar sweep found depths of 199, 200, 208, 210, and so on.

<!-- livebook:{"break_markdown":true} -->

The first order of business is to figure out how quickly the depth increases, just so you know what you're dealing with - you never know if the keys will get carried into deeper water by an ocean current or a fish or something.

To do this, count the number of times a depth measurement increases from the previous measurement. (There is no measurement before the first measurement.) In the example above, the changes are as follows:

```
199 (N/A - no previous measurement)
200 (increased)
208 (increased)
210 (increased)
200 (decreased)
207 (increased)
240 (increased)
269 (increased)
260 (decreased)
263 (increased)
```

In this example, there are 7 measurements that are larger than the previous measurement.

## __ |>Â  Getting Data

We store the input in a text file at location `/data/day-1/input.txt`. The data is a series of newline-chunked integer strings.

<!-- livebook:{"livebook_object":"cell_input","name":"Input File Path:","type":"text","value":"/data/assets/day-1-sonar-input.txt"} -->

```elixir
input_stream =
  "Input File Path:"
  |> IO.gets()
  |> String.trim()
  |> File.stream!()
  |> Stream.map(&(String.trim(&1) |> Integer.parse() |> elem(0)))
```

```output
#Stream<[
  enum: %File.Stream{
    line_or_bytes: :line,
    modes: [:raw, :read_ahead, :binary],
    path: "/data/assets/day-1-sonar-input.txt",
    raw: true
  },
  funs: [#Function<47.58486609/1 in Stream.map/2>]
]>
```

## __ |> Solution

```elixir
input_stream
|> Enum.reduce({0, nil}, fn
  measurement, {0, nil} ->
    {0, measurement}

  measurement, {count, previous_measurement}
  when measurement > previous_measurement ->
    {count + 1, measurement}

  measurement, {count, _} ->
    {count, measurement}
end)
|> elem(0)
|> IO.inspect(label: "Depth Index")
```

```output
Depth Index: 1709
```

```output
1709
```

## |> Part 2 - Aggregated Sonar

Considering every single measurement isn't as useful as you expected: there's just too much noise in the data.

Instead, consider sums of a **three-measurement sliding window**. Again considering the above example:

```none
199  A      
200  A B    
208  A B C  
210    B C D
200  E   C D
207  E F   D
240  E F G  
269    F G H
260      G H
263        H
```

Start by comparing the first and second three-measurement windows. The measurements in the first window are marked `A` (`199`, `200`, `208`); their sum is `199 + 200 + 208 = 607`. The second window is marked `B` (`200`, `208`, `210`); its sum is `618`. The sum of measurements in the second window is larger than the sum of the first, so this first comparison increased.

Your goal now is to count the number of times the sum of measurements in this sliding window increases from the previous sum. So, compare A with B, then compare B with C, then C with D, and so on. Stop when there aren't enough measurements left to create a new three-measurement sum.

In the above example, the sum of each three-measurement window is as follows:

```none
A: 607 (N/A - no previous sum)
B: 618 (increased)
C: 618 (no change)
D: 617 (decreased)
E: 647 (increased)
F: 716 (increased)
G: 769 (increased)
H: 792 (increased)
```

In this example, there are `5` sums that are larger than the previous sum.

Consider sums of a three-measurement sliding window. How many sums are larger than the previous sum?

<!-- livebook:{"branch_parent_index":3} -->

## __ |> Solution

```elixir
input_stream
|> Stream.chunk_every(3, 1, :discard)
|> Stream.map(&Enum.sum/1)
|> Stream.chunk_every(2, 1, :discard)
|> Enum.count(fn
  [first, second] when second > first -> true
  _ -> false
end)
```

```output
1761
```

**04.12.2021 17:00** - I have updated the above code to use Jose Valim's "stream trick".

## Day 2

Now, you need to figure out how to pilot this thing.

## |> Part 1: Reverse-Engineering

It seems like the submarine can take a series of commands like `forward 1`, `down 2`, or `up 3`:

* `forward X` increases the horizontal position by `X` units.
* `down X` **increases** the depth by `X` units.
* `up X` **decreases** the depth by `X` units.

Note that since you're on a submarine, `down` and `up` affect your **depth**, and so they have the opposite result of what you might expect.

The submarine seems to already have a planned course (your puzzle input). You should probably figure out where it's going. For example:

```none
forward 5
down 5
forward 8
up 3
down 8
forward 2
```

Your horizontal position and depth both start at `0`. The steps above would then modify them as follows:

* `forward 5` adds `5` to your horizontal position, a total of `5`.
* `down 5` adds `5` to your depth, resulting in a value of `5`.
* `forward 8` adds `8` to your horizontal position, a total of `13`.
* `up 3` decreases your depth by `3`, resulting in a value of `2`.
* `down 8` adds `8` to your depth, resulting in a value of `10`.
* `forward 2` adds `2` to your horizontal position, a total of `15`.

After following these instructions, you would have a horizontal position of `15` and a depth of `10`. (Multiplying these together produces `150`.)

Calculate the horizontal position and depth you would have after following the planned course. **What do you get if you multiply your final horizontal position by your final depth?**

## __ |> Getting Data

<!-- livebook:{"livebook_object":"cell_input","name":"Input File Path:","type":"text","value":"/data/assets/day-2-directions.txt"} -->

```elixir
input_stream =
  "Input File Path:"
  |> IO.gets()
  |> String.trim()
  |> File.stream!()
  |> Stream.map(
    &(String.trim(&1)
      |> String.split(" ")
      |> (fn [direction, velocity] ->
            {direction, Integer.parse(velocity) |> elem(0)}
          end).())
  )
```

```output
#Stream<[
  enum: %File.Stream{
    line_or_bytes: :line,
    modes: [:raw, :read_ahead, :binary],
    path: "/data/assets/day-2-directions.txt",
    raw: true
  },
  funs: [#Function<47.58486609/1 in Stream.map/2>]
]>
```

## __ |> Solution

```elixir
input_stream
|> Enum.reduce({0, 0}, fn
  {"forward", value}, {acc_x, acc_y} -> {acc_x + value, acc_y}
  {"up", value}, {acc_x, acc_y} -> {acc_x, acc_y - value}
  {"down", value}, {acc_x, acc_y} -> {acc_x, acc_y + value}
  _, acc -> acc
end)
|> IO.inspect(label: "Horizontal and Vertical position")
|> (fn {x, y} -> x * y end).()
|> IO.inspect(label: "Vector result")
```

```output
Horizontal and Vertical position: {2165, 933}
Vector result: 2019945
```

```output
2019945
```

## |> Part 2

Based on your calculations, the planned course doesn't seem to make any sense. You find the submarine manual and discover that the process is actually slightly more complicated.

In addition to horizontal position and depth, you'll also need to track a third value, aim, which also starts at 0. The commands also mean something entirely different than you first thought:

* `down X` **increases** your aim by `X` units.
* `up X` **decreases** your aim by `X` units.
* forward X does two things:
  * It increases your horizontal position by `X` units.
  * It increases your depth by your aim **multiplied** by `X`.

Again note that since you're on a submarine, `down` and `up` do the opposite of what you might expect: "down" means aiming in the positive direction.

Now, the above example does something different:

* `forward 5` adds `5` to your horizontal position, a total of `5`. Because your aim is `0`, your depth does not change.
* `down 5` adds `5` to your aim, resulting in a value of `5`.
* `forward 8` adds `8` to your horizontal position, a total of `13`. Because your aim is `5`, your depth increases by `8*5=40`.
* `up 3` decreases your aim by `3`, resulting in a value of `2`.
* `down 8` adds `8` to your aim, resulting in a value of `10`.
* `forward 2` adds `2` to your horizontal position, a total of `15`. Because your aim is `10`, your depth increases by `2*10=20` to a total of `60`.

After following these new instructions, you would have a horizontal position of `15` and a depth of `60`. (Multiplying these **produces `900`**.)

Using this new interpretation of the commands, calculate the horizontal position and depth you would have after following the planned course. 
**What do you get if you multiply your final horizontal position by your final depth?**

<!-- livebook:{"branch_parent_index":9} -->

## __ |> Solution

```elixir
input_stream
|> Enum.reduce({0, 0, 0}, fn
  {"forward", value}, {acc_x, acc_y, aim} ->
    {acc_x + value, acc_y + aim * value, aim}

  {"up", value}, {acc_x, acc_y, aim} ->
    {acc_x, acc_y, aim - value}

  {"down", value}, {acc_x, acc_y, aim} ->
    {acc_x, acc_y, aim + value}

  _, acc ->
    acc
end)
|> IO.inspect(label: "Horizontal and Vertical position")
|> (fn {x, y, _aim} -> x * y end).()
|> IO.inspect(label: "Vector result")
```

```output
Horizontal and Vertical position: {2165, 738712, 933}
Vector result: 1599311480
```

```output
1599311480
```
